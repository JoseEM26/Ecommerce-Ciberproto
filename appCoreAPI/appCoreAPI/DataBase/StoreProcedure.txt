USE CiberProtoDatabase
GO

-- =============================================
-- PROCEDIMIENTOS PARA USUARIO
-- =============================================

IF OBJECT_ID('usp_listarUsuario') IS NOT NULL DROP PROCEDURE usp_listarUsuario
GO
CREATE PROCEDURE usp_listarUsuario
AS
BEGIN
    SET NOCOUNT ON;
    SELECT IdUsuario, Nombres, Apellidos, Correo, Telefono, Rol, Activo, FechaRegistro
    FROM tb_usuario;
END
GO

IF OBJECT_ID('usp_registrarUsuario') IS NOT NULL DROP PROCEDURE usp_registrarUsuario
GO
CREATE PROCEDURE usp_registrarUsuario
    @Nombres    VARCHAR(255),
    @Apellidos VARCHAR(255),
    @Correo     VARCHAR(255),
    @Clave      VARCHAR(100),
    @Telefono   VARCHAR(20),
    @Rol        VARCHAR(20)
AS
BEGIN
    SET NOCOUNT ON;
    INSERT INTO tb_usuario (Nombres, Apellidos, Correo, Clave, Telefono, Rol)
    VALUES (@Nombres, @Apellidos, @Correo, @Clave, @Telefono, ISNULL(@Rol, 'CLIENTE'));
END
GO

IF OBJECT_ID('usp_obtenerUsuarioById') IS NOT NULL DROP PROCEDURE usp_obtenerUsuarioById
GO
CREATE PROCEDURE usp_obtenerUsuarioById
    @IdUsuario INT
AS
BEGIN
    SET NOCOUNT ON;
    SELECT IdUsuario, Nombres, Apellidos, Correo, Telefono, Rol, Activo
    FROM tb_usuario WHERE IdUsuario = @IdUsuario;
END
GO

IF OBJECT_ID('usp_actualizarUsuario') IS NOT NULL DROP PROCEDURE usp_actualizarUsuario
GO
CREATE PROCEDURE usp_actualizarUsuario
    @IdUsuario INT,
    @Nombres    VARCHAR(255),
    @Apellidos VARCHAR(255),
    @Correo     VARCHAR(255),
    @Telefono   VARCHAR(20),
    @Activo     BIT
AS
BEGIN
    SET NOCOUNT ON;
    IF EXISTS (SELECT 1 FROM tb_usuario WHERE Correo = @Correo AND IdUsuario <> @IdUsuario)
    BEGIN
        RAISERROR('El correo ya está siendo usado por otro usuario', 16, 1);
        RETURN;
    END
    UPDATE tb_usuario
    SET Nombres = @Nombres, Apellidos = @Apellidos, Correo = @Correo, Telefono = @Telefono, Activo = @Activo
    WHERE IdUsuario = @IdUsuario;
END
GO

IF OBJECT_ID('usp_usuario_login') IS NOT NULL DROP PROCEDURE usp_usuario_login
GO
CREATE PROCEDURE usp_usuario_login
    @Correo VARCHAR(255),
    @Clave  VARCHAR(100)
AS
BEGIN
    SET NOCOUNT ON;
    SELECT IdUsuario, Nombres, Apellidos, Correo, Telefono, Rol, Activo
    FROM tb_usuario WHERE Correo = @Correo AND Clave = @Clave AND Activo = 1;
END
GO

IF OBJECT_ID('usp_usuario_eliminar') IS NOT NULL DROP PROCEDURE usp_usuario_eliminar
GO
CREATE PROCEDURE usp_usuario_eliminar
    @IdUsuario INT
AS
BEGIN
    SET NOCOUNT ON;
    UPDATE tb_usuario SET Activo = 0 WHERE IdUsuario = @IdUsuario;
END
GO

-- =============================================
-- CATEGORIA 
-- =============================================
IF OBJECT_ID('sp_ListarCategoria') IS NOT NULL DROP PROCEDURE sp_ListarCategoria
GO
CREATE PROCEDURE sp_ListarCategoria
AS
BEGIN
    SELECT IdCategoria, Descripcion, Activo FROM tb_categoria 
END
GO

IF OBJECT_ID('sp_GuardarCategoria') IS NOT NULL DROP PROCEDURE sp_GuardarCategoria
GO
CREATE PROCEDURE sp_GuardarCategoria
(@Descripcion VARCHAR(255))
AS
BEGIN
    IF NOT EXISTS (SELECT * FROM tb_categoria WHERE Descripcion = @Descripcion)
    BEGIN
        INSERT INTO tb_categoria(Descripcion, Activo) VALUES (@Descripcion, 1)
    END
END
GO

IF OBJECT_ID('sp_EditarCategoria') IS NOT NULL DROP PROCEDURE sp_EditarCategoria
GO
CREATE PROCEDURE sp_EditarCategoria
(@IdCategoria INT, @Descripcion VARCHAR(255), @Activo BIT)
AS
BEGIN
    IF NOT EXISTS (SELECT * FROM tb_categoria WHERE Descripcion = @Descripcion AND IdCategoria != @IdCategoria)
    BEGIN
        UPDATE tb_categoria SET Descripcion = @Descripcion, Activo = @Activo WHERE IdCategoria = @IdCategoria
    END
END
GO

IF OBJECT_ID('sp_EliminarCategoria') IS NOT NULL DROP PROCEDURE sp_EliminarCategoria
GO
CREATE PROCEDURE sp_EliminarCategoria
(@IdCategoria INT)
AS
BEGIN
    UPDATE tb_categoria SET Activo = 0 WHERE IdCategoria = @IdCategoria
END
GO

-- =============================================
-- MARCA 
-- =============================================
IF OBJECT_ID('sp_ListarMarca') IS NOT NULL DROP PROCEDURE sp_ListarMarca
GO
CREATE PROCEDURE sp_ListarMarca
AS
BEGIN
    SELECT IdMarca, Descripcion, Activo FROM tb_marca
END
GO

IF OBJECT_ID('sp_GuardarMarca') IS NOT NULL DROP PROCEDURE sp_GuardarMarca
GO
CREATE PROCEDURE sp_GuardarMarca
(@Descripcion VARCHAR(255))
AS
BEGIN
    IF NOT EXISTS (SELECT * FROM tb_marca WHERE Descripcion = @Descripcion)
    BEGIN
        INSERT INTO tb_marca(Descripcion, Activo) VALUES (@Descripcion, 1)
    END
END
GO

IF OBJECT_ID('sp_EditarMarca') IS NOT NULL DROP PROCEDURE sp_EditarMarca
GO
CREATE PROCEDURE sp_EditarMarca
(@IdMarca INT, @Descripcion VARCHAR(255), @Activo BIT)
AS
BEGIN
    IF NOT EXISTS (SELECT * FROM tb_marca WHERE Descripcion = @Descripcion AND IdMarca != @IdMarca)
    BEGIN
        UPDATE tb_marca SET Descripcion = @Descripcion, Activo = @Activo WHERE IdMarca = @IdMarca
    END
END
GO

IF OBJECT_ID('sp_EliminarMarca') IS NOT NULL DROP PROCEDURE sp_EliminarMarca
GO
CREATE PROCEDURE sp_EliminarMarca
(@IdMarca INT)
AS
BEGIN
    UPDATE tb_marca SET Activo = 0 WHERE IdMarca = @IdMarca
    UPDATE tb_producto SET Activo = 0 WHERE IdMarca = @IdMarca
END
GO

-- =============================================
-- PRODUCTO 
-- =============================================
IF OBJECT_ID('sp_ListarProducto') IS NOT NULL DROP PROCEDURE sp_ListarProducto
GO
CREATE PROCEDURE sp_ListarProducto
AS
BEGIN
    SELECT p.IdProducto, p.Nombre, p.Descripcion,
        p.IdMarca, m.Descripcion AS NombreMarca,
        p.IdCategoria, c.Descripcion AS NombreCategoria,
        p.Precio, p.Stock, p.UrlImagen, p.Activo
    FROM tb_producto p
    INNER JOIN tb_marca m ON p.IdMarca = m.IdMarca
    INNER JOIN tb_categoria c ON p.IdCategoria = c.IdCategoria
END
GO

IF OBJECT_ID('sp_GuardarProducto') IS NOT NULL DROP PROCEDURE sp_GuardarProducto
GO
CREATE PROCEDURE sp_GuardarProducto
(@Nombre VARCHAR(255), @Descripcion VARCHAR(500), @IdMarca INT, @IdCategoria INT, @Precio DECIMAL(10,2), @Stock INT, @UrlImagen VARCHAR(MAX))
AS
BEGIN
    IF NOT EXISTS (SELECT * FROM tb_producto WHERE Nombre = @Nombre)
    BEGIN
        INSERT INTO tb_producto(Nombre, Descripcion, IdMarca, IdCategoria, Precio, Stock, UrlImagen, Activo)
        VALUES (@Nombre, @Descripcion, @IdMarca, @IdCategoria, @Precio, @Stock, @UrlImagen, 1)
    END
END
GO

IF OBJECT_ID('sp_EditarProducto') IS NOT NULL DROP PROCEDURE sp_EditarProducto
GO
CREATE PROCEDURE sp_EditarProducto
(@IdProducto INT, @Nombre VARCHAR(255), @Descripcion VARCHAR(500), @IdMarca INT, @IdCategoria INT, @Precio DECIMAL(10,2), @Stock INT, @UrlImagen VARCHAR(MAX), @Activo BIT)
AS
BEGIN
    IF NOT EXISTS (SELECT * FROM tb_producto WHERE Nombre = @Nombre AND IdProducto != @IdProducto)
    BEGIN
        UPDATE tb_producto SET Nombre = @Nombre, Descripcion = @Descripcion, IdMarca = @IdMarca, IdCategoria = @IdCategoria, Precio = @Precio, Stock = @Stock, UrlImagen = @UrlImagen, Activo = @Activo
        WHERE IdProducto = @IdProducto
    END
END
GO

IF OBJECT_ID('sp_EliminarProducto') IS NOT NULL DROP PROCEDURE sp_EliminarProducto
GO
CREATE PROCEDURE sp_EliminarProducto
(@IdProducto INT)
AS
BEGIN
    UPDATE tb_producto SET Activo = 0 WHERE IdProducto = @IdProducto
END
GO

-- =============================================
-- CARRITO
-- =============================================
IF OBJECT_ID('sp_ListarCarrito') IS NOT NULL DROP PROCEDURE sp_ListarCarrito
GO
CREATE PROCEDURE sp_ListarCarrito
AS
BEGIN
    SELECT c.IdCarrito, c.IdUsuario, c.IdProducto, c.Cantidad,
        p.Nombre AS NombreProducto, p.Descripcion AS DescripcionProducto, p.Precio AS PrecioProducto,
        p.Stock, p.UrlImagen AS ImagenProducto, p.Activo AS ActivoProducto,
        m.Descripcion AS NombreMarca, cat.Descripcion AS NombreCategoria
    FROM tb_carrito c
    INNER JOIN tb_producto p ON c.IdProducto = p.IdProducto
    INNER JOIN tb_marca m ON p.IdMarca = m.IdMarca
    INNER JOIN tb_categoria cat ON p.IdCategoria = cat.IdCategoria
    WHERE p.Activo = 1
    ORDER BY c.IdCarrito DESC
END
GO

IF OBJECT_ID('sp_ListarCarritoPorUsuario') IS NOT NULL DROP PROCEDURE sp_ListarCarritoPorUsuario
GO
CREATE PROCEDURE sp_ListarCarritoPorUsuario
    @IdUsuario INT
AS
BEGIN
    SELECT c.IdCarrito, c.IdUsuario, c.IdProducto, c.Cantidad,
        p.Nombre AS NombreProducto, p.Descripcion AS DescripcionProducto, p.Precio AS PrecioProducto,
        p.Stock, p.UrlImagen AS ImagenProducto, p.Activo AS ActivoProducto,
        m.Descripcion AS NombreMarca, cat.Descripcion AS NombreCategoria
    FROM tb_carrito c
    INNER JOIN tb_producto p ON c.IdProducto = p.IdProducto
    INNER JOIN tb_marca m ON p.IdMarca = m.IdMarca
    INNER JOIN tb_categoria cat ON p.IdCategoria = cat.IdCategoria
    WHERE c.IdUsuario = @IdUsuario AND p.Activo = 1
    ORDER BY c.IdCarrito DESC
END
GO

IF OBJECT_ID('sp_ObtenerCarritoUsuarioProducto') IS NOT NULL DROP PROCEDURE sp_ObtenerCarritoUsuarioProducto
GO
CREATE PROCEDURE sp_ObtenerCarritoUsuarioProducto
    @IdUsuario INT, @IdProducto INT
AS
BEGIN
    SELECT c.IdCarrito, c.IdUsuario, c.IdProducto, c.Cantidad,
        p.Nombre AS NombreProducto, p.Precio AS PrecioProducto, p.UrlImagen AS ImagenProducto
    FROM tb_carrito c
    INNER JOIN tb_producto p ON c.IdProducto = p.IdProducto
    WHERE c.IdUsuario = @IdUsuario AND c.IdProducto = @IdProducto AND p.Activo = 1
END
GO

IF OBJECT_ID('sp_GuardarCarrito') IS NOT NULL DROP PROCEDURE sp_GuardarCarrito
GO
CREATE PROCEDURE sp_GuardarCarrito
    @IdUsuario INT, @IdProducto INT, @Cantidad INT
AS
BEGIN
    BEGIN TRY
        IF NOT EXISTS (SELECT 1 FROM tb_producto WHERE IdProducto = @IdProducto AND Activo = 1)
        BEGIN RAISERROR('Producto no disponible', 16, 1) RETURN END
        
        DECLARE @StockDisponible INT
        SELECT @StockDisponible = Stock FROM tb_producto WHERE IdProducto = @IdProducto
        
        IF @StockDisponible < @Cantidad
        BEGIN RAISERROR('Stock insuficiente', 16, 1) RETURN END
        
        MERGE tb_carrito AS target
        USING (SELECT @IdUsuario, @IdProducto, @Cantidad) AS source (IdUsuario, IdProducto, Cantidad)
        ON (target.IdUsuario = source.IdUsuario AND target.IdProducto = source.IdProducto)
        WHEN MATCHED THEN UPDATE SET Cantidad = source.Cantidad
        WHEN NOT MATCHED THEN INSERT (IdUsuario, IdProducto, Cantidad) VALUES (source.IdUsuario, source.IdProducto, source.Cantidad);
        
        SELECT 'Producto agregado al carrito correctamente' AS Mensaje
    END TRY
    BEGIN CATCH
        SELECT ERROR_MESSAGE() AS Mensaje
    END CATCH
END
GO

IF OBJECT_ID('sp_ActualizarCarrito') IS NOT NULL DROP PROCEDURE sp_ActualizarCarrito
GO
CREATE PROCEDURE sp_ActualizarCarrito
    @IdCarrito INT, @Cantidad INT
AS
BEGIN
    BEGIN TRY
        IF NOT EXISTS (SELECT 1 FROM tb_carrito WHERE IdCarrito = @IdCarrito)
        BEGIN RAISERROR('Item no encontrado en carrito', 16, 1) RETURN END
        
        DECLARE @IdProducto INT
        SELECT @IdProducto = IdProducto FROM tb_carrito WHERE IdCarrito = @IdCarrito
        
        DECLARE @StockDisponible INT
        SELECT @StockDisponible = Stock FROM tb_producto WHERE IdProducto = @IdProducto
        
        IF @StockDisponible < @Cantidad
        BEGIN RAISERROR('Stock insuficiente', 16, 1) RETURN END
        
        UPDATE tb_carrito SET Cantidad = @Cantidad WHERE IdCarrito = @IdCarrito
        SELECT 'Carrito actualizado correctamente' AS Mensaje
    END TRY
    BEGIN CATCH
        SELECT ERROR_MESSAGE() AS Mensaje
    END CATCH
END
GO

IF OBJECT_ID('sp_EliminarCarrito') IS NOT NULL DROP PROCEDURE sp_EliminarCarrito
GO
CREATE PROCEDURE sp_EliminarCarrito
    @IdCarrito INT
AS
BEGIN
    BEGIN TRY
        DELETE FROM tb_carrito WHERE IdCarrito = @IdCarrito
        SELECT 'Item eliminado del carrito' AS Mensaje
    END TRY
    BEGIN CATCH
        SELECT ERROR_MESSAGE() AS Mensaje
    END CATCH
END
GO

IF OBJECT_ID('sp_EliminarCarritoUsuarioProducto') IS NOT NULL DROP PROCEDURE sp_EliminarCarritoUsuarioProducto
GO
CREATE PROCEDURE sp_EliminarCarritoUsuarioProducto
    @IdUsuario INT, @IdProducto INT
AS
BEGIN
    BEGIN TRY
        DELETE FROM tb_carrito WHERE IdUsuario = @IdUsuario AND IdProducto = @IdProducto
        SELECT 'Producto eliminado del carrito' AS Mensaje
    END TRY
    BEGIN CATCH
        SELECT ERROR_MESSAGE() AS Mensaje
    END CATCH
END
GO

IF OBJECT_ID('sp_VaciarCarritoUsuario') IS NOT NULL DROP PROCEDURE sp_VaciarCarritoUsuario
GO
CREATE PROCEDURE sp_VaciarCarritoUsuario
    @IdUsuario INT
AS
BEGIN
    BEGIN TRY
        DELETE FROM tb_carrito WHERE IdUsuario = @IdUsuario
        SELECT 'Carrito vaciado correctamente' AS Mensaje
    END TRY
    BEGIN CATCH
        SELECT ERROR_MESSAGE() AS Mensaje
    END CATCH
END
GO

-- =============================================
-- VENTAS
-- =============================================
IF OBJECT_ID('sp_ListarVenta') IS NOT NULL DROP PROCEDURE sp_ListarVenta
GO
CREATE PROCEDURE sp_ListarVenta
AS
BEGIN
    SELECT v.IdVenta, v.IdUsuario, v.IdTarjeta, v.TotalProductos, v.MontoTotal, v.Contacto, v.Telefono, v.Direccion, v.IdTransaccion, v.FechaVenta,
        u.Nombres + ' ' + u.Apellidos AS NombreUsuario, u.Correo AS CorreoUsuario,
        tp.Tipo AS TipoTarjeta, LEFT(tp.NumeroTarjeta, 4) + '****' + RIGHT(tp.NumeroTarjeta, 4) AS NumeroTarjeta
    FROM tb_venta v
    LEFT JOIN tb_usuario u ON v.IdUsuario = u.IdUsuario
    LEFT JOIN tb_tarjeta_pago tp ON v.IdTarjeta = tp.IdTarjeta
    ORDER BY v.FechaVenta DESC
END
GO

IF OBJECT_ID('sp_ListarVentaPorUsuario') IS NOT NULL DROP PROCEDURE sp_ListarVentaPorUsuario
GO
CREATE PROCEDURE sp_ListarVentaPorUsuario
    @IdUsuario INT
AS
BEGIN
    SELECT v.IdVenta, v.IdUsuario, v.IdTarjeta, v.TotalProductos, v.MontoTotal, v.Contacto, v.Telefono, v.Direccion, v.IdTransaccion, v.FechaVenta,
        u.Nombres + ' ' + u.Apellidos AS NombreUsuario, u.Correo AS CorreoUsuario,
        tp.Tipo AS TipoTarjeta, LEFT(tp.NumeroTarjeta, 4) + '****' + RIGHT(tp.NumeroTarjeta, 4) AS NumeroTarjeta
    FROM tb_venta v
    LEFT JOIN tb_usuario u ON v.IdUsuario = u.IdUsuario
    LEFT JOIN tb_tarjeta_pago tp ON v.IdTarjeta = tp.IdTarjeta
    WHERE v.IdUsuario = @IdUsuario
    ORDER BY v.FechaVenta DESC
END
GO

IF OBJECT_ID('sp_RegistrarVenta') IS NOT NULL DROP PROCEDURE sp_RegistrarVenta
GO
CREATE PROCEDURE sp_RegistrarVenta
    @IdUsuario INT, @IdTarjeta INT = NULL, @Contacto VARCHAR(255), @Telefono VARCHAR(20), @Direccion VARCHAR(MAX), @IdTransaccion VARCHAR(100) = NULL
AS
BEGIN
    BEGIN TRANSACTION
    BEGIN TRY
        IF NOT EXISTS (SELECT 1 FROM tb_usuario WHERE IdUsuario = @IdUsuario AND Activo = 1)
        BEGIN RAISERROR('Usuario no válido', 16, 1) ROLLBACK RETURN END
        
        IF @IdTarjeta IS NOT NULL AND NOT EXISTS (SELECT 1 FROM tb_tarjeta_pago WHERE IdTarjeta = @IdTarjeta AND IdUsuario = @IdUsuario AND Activo = 1)
        BEGIN RAISERROR('Tarjeta no válida', 16, 1) ROLLBACK RETURN END
        
        DECLARE @CarritoItems TABLE (IdProducto INT, Cantidad INT, Precio DECIMAL(10,2), StockDisponible INT)
        INSERT INTO @CarritoItems (IdProducto, Cantidad, Precio, StockDisponible)
        SELECT c.IdProducto, c.Cantidad, p.Precio, p.Stock
        FROM tb_carrito c INNER JOIN tb_producto p ON c.IdProducto = p.IdProducto
        WHERE c.IdUsuario = @IdUsuario AND p.Activo = 1
        
        IF EXISTS (SELECT 1 FROM @CarritoItems WHERE Cantidad > StockDisponible)
        BEGIN RAISERROR('Stock insuficiente para algunos productos', 16, 1) ROLLBACK RETURN END
        
        DECLARE @TotalProductos INT, @MontoTotal DECIMAL(10,2)
        SELECT @TotalProductos = SUM(Cantidad), @MontoTotal = SUM(Cantidad * Precio) FROM @CarritoItems
        
        DECLARE @IdVenta INT
        INSERT INTO tb_venta (IdUsuario, IdTarjeta, TotalProductos, MontoTotal, Contacto, Telefono, Direccion, IdTransaccion)
        VALUES (@IdUsuario, @IdTarjeta, @TotalProductos, @MontoTotal, @Contacto, @Telefono, @Direccion, @IdTransaccion)
        SET @IdVenta = SCOPE_IDENTITY()
        
        INSERT INTO tb_detalle_venta (IdVenta, IdProducto, Cantidad, PrecioUnitario, Total)
        SELECT @IdVenta, IdProducto, Cantidad, Precio, Cantidad * Precio FROM @CarritoItems
        
        UPDATE p SET p.Stock = p.Stock - ci.Cantidad
        FROM tb_producto p INNER JOIN @CarritoItems ci ON p.IdProducto = ci.IdProducto
        
        DELETE FROM tb_carrito WHERE IdUsuario = @IdUsuario
        
        COMMIT TRANSACTION
        SELECT 'Venta registrada correctamente' AS Mensaje, @IdVenta AS IdVentaGenerado, @TotalProductos AS TotalProductos, @MontoTotal AS MontoTotal
    END TRY
    BEGIN CATCH
        ROLLBACK TRANSACTION
        SELECT ERROR_MESSAGE() AS Mensaje, 0 AS IdVentaGenerado
    END CATCH
END
GO

IF OBJECT_ID('sp_ListarDetalleVenta') IS NOT NULL DROP PROCEDURE sp_ListarDetalleVenta
GO
CREATE PROCEDURE sp_ListarDetalleVenta
    @IdVenta INT
AS
BEGIN
    SELECT dv.IdDetalleVenta, dv.IdVenta, dv.IdProducto, dv.Cantidad, dv.PrecioUnitario, dv.Total,
        p.Nombre AS NombreProducto, p.Descripcion AS DescripcionProducto, p.UrlImagen AS ImagenProducto,
        m.Descripcion AS NombreMarca, cat.Descripcion AS NombreCategoria
    FROM tb_detalle_venta dv
    INNER JOIN tb_producto p ON dv.IdProducto = p.IdProducto
    INNER JOIN tb_marca m ON p.IdMarca = m.IdMarca
    INNER JOIN tb_categoria cat ON p.IdCategoria = cat.IdCategoria
    WHERE dv.IdVenta = @IdVenta
    ORDER BY dv.IdDetalleVenta
END
GO

IF OBJECT_ID('sp_ProductosPorCategoria') IS NOT NULL DROP PROCEDURE sp_ProductosPorCategoria
GO
CREATE PROCEDURE sp_ProductosPorCategoria
    @IdCategoria INT, @Pagina INT = 1, @RegistrosPorPagina INT = 12
AS
BEGIN
    SELECT p.IdProducto, p.Nombre, p.Descripcion, p.Precio, p.Stock, p.UrlImagen, p.Activo,
        m.Descripcion AS NombreMarca, cat.Descripcion AS NombreCategoria, COUNT(*) OVER() AS TotalRegistros
    FROM tb_producto p
    INNER JOIN tb_marca m ON p.IdMarca = m.IdMarca
    INNER JOIN tb_categoria cat ON p.IdCategoria = cat.IdCategoria
    WHERE p.IdCategoria = @IdCategoria AND p.Activo = 1 AND cat.Activo = 1
    ORDER BY p.Nombre
    OFFSET (@Pagina - 1) * @RegistrosPorPagina ROWS FETCH NEXT @RegistrosPorPagina ROWS ONLY
END
GO

IF OBJECT_ID('sp_ProductosPorMarca') IS NOT NULL DROP PROCEDURE sp_ProductosPorMarca
GO
CREATE PROCEDURE sp_ProductosPorMarca
    @IdMarca INT, @Pagina INT = 1, @RegistrosPorPagina INT = 12
AS
BEGIN
    SELECT p.IdProducto, p.Nombre, p.Descripcion, p.Precio, p.Stock, p.UrlImagen, p.Activo,
        m.Descripcion AS NombreMarca, cat.Descripcion AS NombreCategoria, COUNT(*) OVER() AS TotalRegistros
    FROM tb_producto p
    INNER JOIN tb_marca m ON p.IdMarca = m.IdMarca
    INNER JOIN tb_categoria cat ON p.IdCategoria = cat.IdCategoria
    WHERE p.IdMarca = @IdMarca AND p.Activo = 1 AND m.Activo = 1
    ORDER BY p.Nombre
    OFFSET (@Pagina - 1) * @RegistrosPorPagina ROWS FETCH NEXT @RegistrosPorPagina ROWS ONLY
END
GO

IF OBJECT_ID('sp_ProductoDetalle') IS NOT NULL DROP PROCEDURE sp_ProductoDetalle
GO
CREATE PROCEDURE sp_ProductoDetalle
    @IdProducto INT
AS
BEGIN
    SELECT p.IdProducto, p.Nombre, p.Descripcion, p.Precio, p.Stock, p.UrlImagen, p.Activo,
        m.IdMarca, m.Descripcion AS NombreMarca, cat.IdCategoria, cat.Descripcion AS NombreCategoria,
        (SELECT COUNT(*) FROM tb_carrito WHERE IdProducto = p.IdProducto) AS EnCarritos,
        (SELECT COUNT(*) FROM tb_detalle_venta WHERE IdProducto = p.IdProducto) AS VecesVendido
    FROM tb_producto p
    INNER JOIN tb_marca m ON p.IdMarca = m.IdMarca
    INNER JOIN tb_categoria cat ON p.IdCategoria = cat.IdCategoria
    WHERE p.IdProducto = @IdProducto AND p.Activo = 1
END
GO

IF OBJECT_ID('sp_VerificarTarjetasUsuario') IS NOT NULL DROP PROCEDURE sp_VerificarTarjetasUsuario
GO
CREATE PROCEDURE sp_VerificarTarjetasUsuario
    @IdUsuario INT
AS
BEGIN
    SELECT CASE WHEN EXISTS (SELECT 1 FROM tb_tarjeta_pago WHERE IdUsuario = @IdUsuario AND Activo = 1) THEN 'SI' ELSE 'NO' END AS TieneTarjetas,
        COUNT(*) AS CantidadTarjetas
    FROM tb_tarjeta_pago WHERE IdUsuario = @IdUsuario AND Activo = 1
END
GO

IF OBJECT_ID('sp_ListarTarjetasUsuario') IS NOT NULL DROP PROCEDURE sp_ListarTarjetasUsuario
GO
CREATE PROCEDURE sp_ListarTarjetasUsuario
    @IdUsuario INT
AS
BEGIN
    SELECT IdTarjeta, Tipo, LEFT(NumeroTarjeta, 4) + '****' + RIGHT(NumeroTarjeta, 4) AS NumeroTarjeta, Titular, FechaExpiracion, Predeterminada
    FROM tb_tarjeta_pago WHERE IdUsuario = @IdUsuario AND Activo = 1
    ORDER BY Predeterminada DESC
END
GO

IF OBJECT_ID('sp_VerificarUsuarioLogueado') IS NOT NULL DROP PROCEDURE sp_VerificarUsuarioLogueado
GO
CREATE PROCEDURE sp_VerificarUsuarioLogueado
    @Correo VARCHAR(255)
AS
BEGIN
    SELECT u.IdUsuario, u.Nombres, u.Apellidos, u.Correo, u.Rol, u.Activo,
        CASE WHEN EXISTS (SELECT 1 FROM tb_tarjeta_pago WHERE IdUsuario = u.IdUsuario AND Activo = 1) THEN 1 ELSE 0 END AS TieneTarjetas,
        (SELECT COUNT(*) FROM tb_carrito WHERE IdUsuario = u.IdUsuario) AS ItemsCarrito
    FROM tb_usuario u WHERE u.Correo = @Correo AND u.Activo = 1
END
GO


-- =============================================
-- 1. PRODUCTOS (Protegemos UrlImagen y Descripcion)
-- =============================================
IF OBJECT_ID('sp_ListarProductogrpc') IS NOT NULL DROP PROCEDURE sp_ListarProductogrpc
GO
CREATE PROCEDURE sp_ListarProductogrpc
AS
BEGIN
    SET NOCOUNT ON;
    SELECT 
        p.IdProducto, 
        p.Nombre, 
        -- Si Descripcion es NULL, devolvemos cadena vacía
        ISNULL(p.Descripcion, '') AS Descripcion, 
        p.IdMarca, 
        m.Descripcion AS Marca,
        p.IdCategoria, 
        c.Descripcion AS Categoria, 
        CONVERT(varchar, p.Precio) as Precio, 
        p.Stock, 
        -- VITAL: Si UrlImagen es NULL, devolvemos cadena vacía
        ISNULL(p.UrlImagen, '') AS UrlImagen
    FROM tb_producto p
    INNER JOIN tb_marca m ON p.IdMarca = m.IdMarca
    INNER JOIN tb_categoria c ON p.IdCategoria = c.IdCategoria
    WHERE p.Activo = 1;
END
GO

IF OBJECT_ID('sp_ListarProductoPorMarcagrpc') IS NOT NULL DROP PROCEDURE sp_ListarProductoPorMarcagrpc
GO
CREATE PROCEDURE sp_ListarProductoPorMarcagrpc
   @IdMarca INT
AS
BEGIN
    SET NOCOUNT ON;
    SELECT 
        p.IdProducto, 
        p.Nombre, 
        ISNULL(p.Descripcion, '') AS Descripcion, 
        p.IdMarca, 
        m.Descripcion AS Marca,
        p.IdCategoria, 
        c.Descripcion AS Categoria, 
        CONVERT(varchar, p.Precio) AS Precio, 
        p.Stock, 
        ISNULL(p.UrlImagen, '') AS UrlImagen
    FROM tb_producto p
    INNER JOIN tb_marca m ON p.IdMarca = m.IdMarca
    INNER JOIN tb_categoria c ON p.IdCategoria = c.IdCategoria
    WHERE p.Activo = 1 AND p.IdMarca = @IdMarca
END
GO

-- =============================================
-- 2. VENTAS (Protegemos IdTarjeta y IdTransaccion)
-- =============================================
IF OBJECT_ID('sp_ListarVentagrpc') IS NOT NULL DROP PROCEDURE sp_ListarVentagrpc
GO
CREATE PROCEDURE sp_ListarVentagrpc
AS
BEGIN
    SELECT 
        v.IdVenta, 
        v.IdUsuario, 
        -- Si pagó en efectivo (NULL), devolvemos 0 para que no falle el Int32 en C#
        ISNULL(v.IdTarjeta, 0) AS IdTarjeta, 
        v.TotalProductos, 
        CONVERT(varchar, v.MontoTotal) AS MontoTotal,
        v.Contacto, 
        v.Telefono, 
        v.Direccion, 
        -- Si no hay código de transacción, devolvemos vacío
        ISNULL(v.IdTransaccion, '') AS IdTransaccion, 
        CONVERT(varchar, v.FechaVenta) AS FechaVenta,
        u.Nombres + ' ' + u.Apellidos AS NombreUsuario, 
        p.Nombre AS NombreProducto    
    FROM tb_venta v
    INNER JOIN tb_detalle_venta dv ON v.IdVenta = dv.IdVenta
    INNER JOIN tb_producto p ON dv.IdProducto = p.IdProducto
    INNER JOIN tb_usuario u ON v.IdUsuario = u.IdUsuario
    WHERE u.Rol = 'CLIENTE'
    ORDER BY v.FechaVenta DESC 
END
GO

-- =============================================
-- 3. USUARIOS (Protegemos Telefono)
-- =============================================
IF OBJECT_ID('sp_ListarUsuariogrpc') IS NOT NULL DROP PROCEDURE sp_ListarUsuariogrpc
GO
CREATE PROCEDURE sp_ListarUsuariogrpc
AS
BEGIN
    SET NOCOUNT ON;
    SELECT 
        u.IdUsuario, 
        u.Nombres, 
        u.Apellidos, 
        u.Correo, 
        -- Muchos usuarios no tienen teléfono, esto evita el crash
        ISNULL(u.Telefono, '') AS Telefono, 
        u.Rol,
        CONVERT(varchar, Activo) AS Activo, 
        CONVERT(varchar, FechaRegistro) AS FechaRegistro
    FROM tb_usuario u
    ORDER BY Nombres, Apellidos;
END
GO