<<<<<<< HEAD
﻿
USE CiberProtoDatabase
GO

-- =============================================
-- PROCEDIMIENTOS PARA USUARIO
-- =============================================

--listar usuarios
CREATE OR ALTER PROCEDURE usp_listarUsuario
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        IdUsuario,
        Nombres,
        Apellidos,
        Correo,
        Telefono,
        Rol,
        Activo,
        FechaRegistro
    FROM tb_usuario;
END
GO

--registrar usuarios
CREATE OR ALTER PROCEDURE usp_registrarUsuario
    @Nombres   VARCHAR(255),
    @Apellidos VARCHAR(255),
    @Correo    VARCHAR(255),
    @Clave     VARCHAR(100),
    @Telefono  VARCHAR(20),
    @Rol       VARCHAR(20)
AS
BEGIN
    SET NOCOUNT ON;

    INSERT INTO tb_usuario (
        Nombres,
        Apellidos,
        Correo,
        Clave,
        Telefono,
        Rol
    )
    VALUES (
        @Nombres,
        @Apellidos,
        @Correo,
        @Clave,
        @Telefono,
        ISNULL(@Rol, 'CLIENTE')
    );
END
GO

--obtener usuario por id
CREATE OR ALTER PROCEDURE usp_obtenerUsuarioById
    @IdUsuario INT
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        IdUsuario,
        Nombres,
        Apellidos,
        Correo,
        Telefono,
        Rol,
        Activo
    FROM tb_usuario
    WHERE IdUsuario = @IdUsuario;
END
GO

-- actualizar usuario
CREATE OR ALTER PROCEDURE usp_actualizarUsuario
    @IdUsuario INT,
    @Nombres   VARCHAR(255),
    @Apellidos VARCHAR(255),
    @Correo    VARCHAR(255),
    @Telefono  VARCHAR(20),
    @Activo    BIT
AS
BEGIN
    SET NOCOUNT ON;

    -- Validar correo duplicado en otro usuario
    IF EXISTS (
        SELECT 1
        FROM tb_usuario
        WHERE Correo = @Correo
          AND IdUsuario <> @IdUsuario
    )
    BEGIN
        RAISERROR('El correo ya está siendo usado por otro usuario', 16, 1);
        RETURN;
    END

    UPDATE tb_usuario
    SET
        Nombres   = @Nombres,
        Apellidos = @Apellidos,
        Correo    = @Correo,
        Telefono  = @Telefono,
        Activo    = @Activo
    WHERE IdUsuario = @IdUsuario;
END
GO
-- login
CREATE OR ALTER PROCEDURE usp_usuario_login
    @Correo VARCHAR(255),
    @Clave  VARCHAR(100)
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        IdUsuario,
        Nombres,
        Apellidos,
        Correo,
        Telefono,
        Rol,
        Activo
    FROM tb_usuario
    WHERE Correo = @Correo
      AND Clave  = @Clave
      AND Activo = 1;
END
GO
--eliminar
CREATE PROCEDURE sp_usuario_eliminar
    @IdUsuario INT
AS
BEGIN
    SET NOCOUNT ON;

    UPDATE tb_usuario
    SET Activo = 0
    WHERE IdUsuario = @IdUsuario;
END
GO

-- =============================================
-- CATEGORIA 
-- =============================================
CREATE OR ALTER PROCEDURE sp_ListarCategoria
AS
BEGIN
    SELECT IdCategoria, Descripcion, Activo FROM tb_categoria 
END
GO

CREATE OR ALTER PROCEDURE sp_GuardarCategoria
(
    @Descripcion VARCHAR(255)
)
AS
BEGIN
    -- Solo inserta si no existe duplicado
    IF NOT EXISTS (SELECT * FROM tb_categoria WHERE Descripcion = @Descripcion)
    BEGIN
        INSERT INTO tb_categoria(Descripcion, Activo) VALUES (@Descripcion, 1)
    END
END
GO

CREATE OR ALTER PROCEDURE sp_EditarCategoria
(
    @IdCategoria INT,
    @Descripcion VARCHAR(255),
    @Activo BIT
)
AS
BEGIN
    IF NOT EXISTS (SELECT * FROM tb_categoria WHERE Descripcion = @Descripcion AND IdCategoria != @IdCategoria)
    BEGIN
        UPDATE tb_categoria SET 
            Descripcion = @Descripcion,
            Activo = @Activo
        WHERE IdCategoria = @IdCategoria
    END
END
GO

CREATE OR ALTER PROCEDURE sp_EliminarCategoria
(
    @IdCategoria INT
)
AS
BEGIN
    -- Borrado lógico simple
    UPDATE tb_categoria SET Activo = 0 WHERE IdCategoria = @IdCategoria
END
GO

-- =============================================
-- MARCA 
-- =============================================
CREATE OR ALTER PROCEDURE sp_ListarMarca
AS
BEGIN
    SELECT IdMarca, Descripcion, Activo FROM tb_marca
END
GO

CREATE OR ALTER PROCEDURE sp_GuardarMarca
(
    @Descripcion VARCHAR(255)
)
AS
BEGIN
    IF NOT EXISTS (SELECT * FROM tb_marca WHERE Descripcion = @Descripcion)
    BEGIN
        INSERT INTO tb_marca(Descripcion, Activo) VALUES (@Descripcion, 1)
    END
END
GO

CREATE OR ALTER PROCEDURE sp_EditarMarca
(
    @IdMarca INT,
    @Descripcion VARCHAR(255),
    @Activo BIT
)
AS
BEGIN
    IF NOT EXISTS (SELECT * FROM tb_marca WHERE Descripcion = @Descripcion AND IdMarca != @IdMarca)
    BEGIN
        UPDATE tb_marca SET 
            Descripcion = @Descripcion,
            Activo = @Activo
        WHERE IdMarca = @IdMarca
    END
END
GO

CREATE OR ALTER PROCEDURE sp_EliminarMarca
(
    @IdMarca INT
)
AS
BEGIN
    -- Cascada: Desactivamos marca y sus productos
    UPDATE tb_marca SET Activo = 0 WHERE IdMarca = @IdMarca
    UPDATE tb_producto SET Activo = 0 WHERE IdMarca = @IdMarca
END
GO

-- =============================================
-- PRODUCTO 
-- =============================================
CREATE OR ALTER PROCEDURE sp_ListarProducto
AS
BEGIN
    SELECT 
        p.IdProducto, 
        p.Nombre, 
        p.Descripcion,
        p.IdMarca, m.Descripcion AS NombreMarca,
        p.IdCategoria, c.Descripcion AS NombreCategoria,
        p.Precio, 
        p.Stock, 
        p.UrlImagen, 
        p.Activo
    FROM tb_producto p
    INNER JOIN tb_marca m ON p.IdMarca = m.IdMarca
    INNER JOIN tb_categoria c ON p.IdCategoria = c.IdCategoria
END
GO

CREATE OR ALTER PROCEDURE sp_GuardarProducto
(
    @Nombre VARCHAR(255),
    @Descripcion VARCHAR(500),
    @IdMarca INT,
    @IdCategoria INT,
    @Precio DECIMAL(10,2),
    @Stock INT,
    @UrlImagen VARCHAR(MAX)
)
AS
BEGIN
    IF NOT EXISTS (SELECT * FROM tb_producto WHERE Nombre = @Nombre)
    BEGIN
        INSERT INTO tb_producto(Nombre, Descripcion, IdMarca, IdCategoria, Precio, Stock, UrlImagen, Activo)
        VALUES (@Nombre, @Descripcion, @IdMarca, @IdCategoria, @Precio, @Stock, @UrlImagen, 1)
    END
END
GO

CREATE OR ALTER PROCEDURE sp_EditarProducto
(
    @IdProducto INT,
    @Nombre VARCHAR(255),
    @Descripcion VARCHAR(500),
    @IdMarca INT,
    @IdCategoria INT,
    @Precio DECIMAL(10,2),
    @Stock INT,
    @UrlImagen VARCHAR(MAX),
    @Activo BIT
)
AS
BEGIN
    IF NOT EXISTS (SELECT * FROM tb_producto WHERE Nombre = @Nombre AND IdProducto != @IdProducto)
    BEGIN
        UPDATE tb_producto SET 
            Nombre = @Nombre,
            Descripcion = @Descripcion,
            IdMarca = @IdMarca,
            IdCategoria = @IdCategoria,
            Precio = @Precio,
            Stock = @Stock,
            UrlImagen = @UrlImagen,
            Activo = @Activo
        WHERE IdProducto = @IdProducto
    END
END
GO

CREATE OR ALTER PROCEDURE sp_EliminarProducto
(
    @IdProducto INT
)
AS
BEGIN
    UPDATE tb_producto SET Activo = 0 WHERE IdProducto = @IdProducto
>>>>>>> main
END
GO


/*-----------------------------------------PROCEDURES DE CARRITO-----------------------------------------*/
-- Listar todo el carrito
CREATE PROCEDURE sp_ListarCarrito
AS
BEGIN
    SELECT 
        c.IdCarrito,
        c.IdUsuario,
        c.IdProducto,
        c.Cantidad,
        p.Nombre AS NombreProducto,
        p.Descripcion AS DescripcionProducto,
        p.Precio AS PrecioProducto,
        p.Stock,
        p.UrlImagen AS ImagenProducto,
        p.Activo AS ActivoProducto,
        m.Descripcion AS NombreMarca,
        cat.Descripcion AS NombreCategoria
    FROM tb_carrito c
    INNER JOIN tb_producto p ON c.IdProducto = p.IdProducto
    INNER JOIN tb_marca m ON p.IdMarca = m.IdMarca
    INNER JOIN tb_categoria cat ON p.IdCategoria = cat.IdCategoria
    WHERE p.Activo = 1
    ORDER BY c.IdCarrito DESC
END
GO

-- Listar carrito por usuario
CREATE PROCEDURE sp_ListarCarritoPorUsuario
    @IdUsuario INT
AS
BEGIN
    SELECT 
        c.IdCarrito,
        c.IdUsuario,
        c.IdProducto,
        c.Cantidad,
        p.Nombre AS NombreProducto,
        p.Descripcion AS DescripcionProducto,
        p.Precio AS PrecioProducto,
        p.Stock,
        p.UrlImagen AS ImagenProducto,
        p.Activo AS ActivoProducto,
        m.Descripcion AS NombreMarca,
        cat.Descripcion AS NombreCategoria
    FROM tb_carrito c
    INNER JOIN tb_producto p ON c.IdProducto = p.IdProducto
    INNER JOIN tb_marca m ON p.IdMarca = m.IdMarca
    INNER JOIN tb_categoria cat ON p.IdCategoria = cat.IdCategoria
    WHERE c.IdUsuario = @IdUsuario 
        AND p.Activo = 1
    ORDER BY c.IdCarrito DESC
END
GO

-- Obtener item específico del carrito
CREATE PROCEDURE sp_ObtenerCarritoUsuarioProducto
    @IdUsuario INT,
    @IdProducto INT
AS
BEGIN
    SELECT 
        c.IdCarrito,
        c.IdUsuario,
        c.IdProducto,
        c.Cantidad,
        p.Nombre AS NombreProducto,
        p.Precio AS PrecioProducto,
        p.UrlImagen AS ImagenProducto
    FROM tb_carrito c
    INNER JOIN tb_producto p ON c.IdProducto = p.IdProducto
    WHERE c.IdUsuario = @IdUsuario 
        AND c.IdProducto = @IdProducto
        AND p.Activo = 1
END
GO

-- Agregar o actualizar carrito (MERGE para evitar duplicados)
CREATE PROCEDURE sp_GuardarCarrito
    @IdUsuario INT,
    @IdProducto INT,
    @Cantidad INT
AS
BEGIN
    BEGIN TRY
        -- Verificar si el producto existe y está activo
        IF NOT EXISTS (SELECT 1 FROM tb_producto WHERE IdProducto = @IdProducto AND Activo = 1)
        BEGIN
            RAISERROR('Producto no disponible', 16, 1)
            RETURN
        END
        
        -- Verificar stock
        DECLARE @StockDisponible INT
        SELECT @StockDisponible = Stock FROM tb_producto WHERE IdProducto = @IdProducto
        
        IF @StockDisponible < @Cantidad
        BEGIN
            RAISERROR('Stock insuficiente', 16, 1)
            RETURN
        END
        
        -- MERGE para insertar o actualizar
        MERGE tb_carrito AS target
        USING (SELECT @IdUsuario, @IdProducto, @Cantidad) AS source (IdUsuario, IdProducto, Cantidad)
        ON (target.IdUsuario = source.IdUsuario AND target.IdProducto = source.IdProducto)
        WHEN MATCHED THEN
            UPDATE SET Cantidad = source.Cantidad
        WHEN NOT MATCHED THEN
            INSERT (IdUsuario, IdProducto, Cantidad) 
            VALUES (source.IdUsuario, source.IdProducto, source.Cantidad);
        
        SELECT 'Producto agregado al carrito correctamente' AS Mensaje
    END TRY
    BEGIN CATCH
        SELECT ERROR_MESSAGE() AS Mensaje
    END CATCH
END
GO

-- Actualizar cantidad en carrito
CREATE PROCEDURE sp_ActualizarCarrito
    @IdCarrito INT,
    @Cantidad INT
AS
BEGIN
    BEGIN TRY
        -- Verificar que el carrito existe
        IF NOT EXISTS (SELECT 1 FROM tb_carrito WHERE IdCarrito = @IdCarrito)
        BEGIN
            RAISERROR('Item no encontrado en carrito', 16, 1)
            RETURN
        END
        
        -- Obtener el producto para verificar stock
        DECLARE @IdProducto INT
        SELECT @IdProducto = IdProducto FROM tb_carrito WHERE IdCarrito = @IdCarrito
        
        DECLARE @StockDisponible INT
        SELECT @StockDisponible = Stock FROM tb_producto WHERE IdProducto = @IdProducto
        
        IF @StockDisponible < @Cantidad
        BEGIN
            RAISERROR('Stock insuficiente', 16, 1)
            RETURN
        END
        
        UPDATE tb_carrito 
        SET Cantidad = @Cantidad 
        WHERE IdCarrito = @IdCarrito
        
        SELECT 'Carrito actualizado correctamente' AS Mensaje
    END TRY
    BEGIN CATCH
        SELECT ERROR_MESSAGE() AS Mensaje
    END CATCH
END
GO

-- Eliminar item del carrito
CREATE PROCEDURE sp_EliminarCarrito
    @IdCarrito INT
AS
BEGIN
    BEGIN TRY
        DELETE FROM tb_carrito WHERE IdCarrito = @IdCarrito
        SELECT 'Item eliminado del carrito' AS Mensaje
    END TRY
    BEGIN CATCH
        SELECT ERROR_MESSAGE() AS Mensaje
    END CATCH
END
GO

-- Eliminar por usuario y producto
CREATE PROCEDURE sp_EliminarCarritoUsuarioProducto
    @IdUsuario INT,
    @IdProducto INT
AS
BEGIN
    BEGIN TRY
        DELETE FROM tb_carrito 
        WHERE IdUsuario = @IdUsuario AND IdProducto = @IdProducto
        
        SELECT 'Producto eliminado del carrito' AS Mensaje
    END TRY
    BEGIN CATCH
        SELECT ERROR_MESSAGE() AS Mensaje
    END CATCH
END
GO

-- Vaciar carrito de un usuario
CREATE PROCEDURE sp_VaciarCarritoUsuario
    @IdUsuario INT
AS
BEGIN
    BEGIN TRY
        DELETE FROM tb_carrito WHERE IdUsuario = @IdUsuario
        SELECT 'Carrito vaciado correctamente' AS Mensaje
    END TRY
    BEGIN CATCH
        SELECT ERROR_MESSAGE() AS Mensaje
    END CATCH
END
GO


/*-----------------------------------------AHORA PROCEDURES PARA VENTAS-----------------------------------------*/

-- Listar todas las ventas
CREATE PROCEDURE sp_ListarVenta
AS
BEGIN
    SELECT 
        v.IdVenta,
        v.IdUsuario,
        v.IdTarjeta,
        v.TotalProductos,
        v.MontoTotal,
        v.Contacto,
        v.Telefono,
        v.Direccion,
        v.IdTransaccion,
        v.FechaVenta,
        u.Nombres + ' ' + u.Apellidos AS NombreUsuario,
        u.Correo AS CorreoUsuario,
        tp.Tipo AS TipoTarjeta,
        LEFT(tp.NumeroTarjeta, 4) + '****' + RIGHT(tp.NumeroTarjeta, 4) AS NumeroTarjeta
    FROM tb_venta v
    LEFT JOIN tb_usuario u ON v.IdUsuario = u.IdUsuario
    LEFT JOIN tb_tarjeta_pago tp ON v.IdTarjeta = tp.IdTarjeta
    ORDER BY v.FechaVenta DESC
END
GO

-- Listar ventas por usuario
CREATE PROCEDURE sp_ListarVentaPorUsuario
    @IdUsuario INT
AS
BEGIN
    SELECT 
        v.IdVenta,
        v.IdUsuario,
        v.IdTarjeta,
        v.TotalProductos,
        v.MontoTotal,
        v.Contacto,
        v.Telefono,
        v.Direccion,
        v.IdTransaccion,
        v.FechaVenta,
        u.Nombres + ' ' + u.Apellidos AS NombreUsuario,
        u.Correo AS CorreoUsuario,
        tp.Tipo AS TipoTarjeta,
        LEFT(tp.NumeroTarjeta, 4) + '****' + RIGHT(tp.NumeroTarjeta, 4) AS NumeroTarjeta
    FROM tb_venta v
    LEFT JOIN tb_usuario u ON v.IdUsuario = u.IdUsuario
    LEFT JOIN tb_tarjeta_pago tp ON v.IdTarjeta = tp.IdTarjeta
    WHERE v.IdUsuario = @IdUsuario
    ORDER BY v.FechaVenta DESC
END
GO

--------- Registrar venta (con validación de carrito y stock)
CREATE PROCEDURE sp_RegistrarVenta
    @IdUsuario INT,
    @IdTarjeta INT = NULL,
    @Contacto VARCHAR(255),
    @Telefono VARCHAR(20),
    @Direccion VARCHAR(MAX),
    @IdTransaccion VARCHAR(100) = NULL
AS
BEGIN
    BEGIN TRANSACTION
    BEGIN TRY
        -- Validar que el usuario existe
        IF NOT EXISTS (SELECT 1 FROM tb_usuario WHERE IdUsuario = @IdUsuario AND Activo = 1)
        BEGIN
            RAISERROR('Usuario no válido', 16, 1)
            ROLLBACK
            RETURN
        END
        
        -- Validar tarjeta si se proporciona
        IF @IdTarjeta IS NOT NULL 
            AND NOT EXISTS (SELECT 1 FROM tb_tarjeta_pago WHERE IdTarjeta = @IdTarjeta AND IdUsuario = @IdUsuario AND Activo = 1)
        BEGIN
            RAISERROR('Tarjeta no válida', 16, 1)
            ROLLBACK
            RETURN
        END
        
        -- Obtener items del carrito
        DECLARE @CarritoItems TABLE (
            IdProducto INT,
            Cantidad INT,
            Precio DECIMAL(10,2),
            StockDisponible INT
        )
        
        INSERT INTO @CarritoItems (IdProducto, Cantidad, Precio, StockDisponible)
        SELECT 
            c.IdProducto,
            c.Cantidad,
            p.Precio,
            p.Stock
        FROM tb_carrito c
        INNER JOIN tb_producto p ON c.IdProducto = p.IdProducto
        WHERE c.IdUsuario = @IdUsuario AND p.Activo = 1
        
        -- Validar stock
        IF EXISTS (SELECT 1 FROM @CarritoItems WHERE Cantidad > StockDisponible)
        BEGIN
            RAISERROR('Stock insuficiente para algunos productos', 16, 1)
            ROLLBACK
            RETURN
        END
        
        -- Calcular totales
        DECLARE @TotalProductos INT
        DECLARE @MontoTotal DECIMAL(10,2)
        
        SELECT 
            @TotalProductos = SUM(Cantidad),
            @MontoTotal = SUM(Cantidad * Precio)
        FROM @CarritoItems
        
        -- Insertar venta
        DECLARE @IdVenta INT
        
        INSERT INTO tb_venta (
            IdUsuario, IdTarjeta, TotalProductos, MontoTotal, 
            Contacto, Telefono, Direccion, IdTransaccion
        )
        VALUES (
            @IdUsuario, @IdTarjeta, @TotalProductos, @MontoTotal,
            @Contacto, @Telefono, @Direccion, @IdTransaccion
        )
        
        SET @IdVenta = SCOPE_IDENTITY()
        
        -- Insertar detalles y actualizar stock
        INSERT INTO tb_detalle_venta (IdVenta, IdProducto, Cantidad, PrecioUnitario, Total)
        SELECT 
            @IdVenta,
            IdProducto,
            Cantidad,
            Precio,
            Cantidad * Precio
        FROM @CarritoItems
        
        -- Actualizar stock de productos
        UPDATE p
        SET p.Stock = p.Stock - ci.Cantidad
        FROM tb_producto p
        INNER JOIN @CarritoItems ci ON p.IdProducto = ci.IdProducto
        
        -- Vaciar carrito del usuario
        DELETE FROM tb_carrito WHERE IdUsuario = @IdUsuario
        
        COMMIT TRANSACTION
        
        -- Retornar resultado
        SELECT 
            'Venta registrada correctamente' AS Mensaje,
            @IdVenta AS IdVentaGenerado,
            @TotalProductos AS TotalProductos,
            @MontoTotal AS MontoTotal
        
    END TRY
    BEGIN CATCH
        ROLLBACK TRANSACTION
        SELECT ERROR_MESSAGE() AS Mensaje, 0 AS IdVentaGenerado
    END CATCH
END
GO

--------- Aqui termina el procedure sp_RegistrarVenta

--- Listar detalles de una venta
CREATE PROCEDURE sp_ListarDetalleVenta
    @IdVenta INT
AS
BEGIN
    SELECT 
        dv.IdDetalleVenta,
        dv.IdVenta,
        dv.IdProducto,
        dv.Cantidad,
        dv.PrecioUnitario,
        dv.Total,
        p.Nombre AS NombreProducto,
        p.Descripcion AS DescripcionProducto,
        p.UrlImagen AS ImagenProducto,
        m.Descripcion AS NombreMarca,
        cat.Descripcion AS NombreCategoria
    FROM tb_detalle_venta dv
    INNER JOIN tb_producto p ON dv.IdProducto = p.IdProducto
    INNER JOIN tb_marca m ON p.IdMarca = m.IdMarca
    INNER JOIN tb_categoria cat ON p.IdCategoria = cat.IdCategoria
    WHERE dv.IdVenta = @IdVenta
    ORDER BY dv.IdDetalleVenta
END
GO


-- Productos por categoría (con paginación)
CREATE PROCEDURE sp_ProductosPorCategoria
    @IdCategoria INT,
    @Pagina INT = 1,
    @RegistrosPorPagina INT = 12
AS
BEGIN
    SELECT 
        p.IdProducto,
        p.Nombre,
        p.Descripcion,
        p.Precio,
        p.Stock,
        p.UrlImagen,
        p.Activo,
        m.Descripcion AS NombreMarca,
        cat.Descripcion AS NombreCategoria,
        -- Para paginación
        COUNT(*) OVER() AS TotalRegistros
    FROM tb_producto p
    INNER JOIN tb_marca m ON p.IdMarca = m.IdMarca
    INNER JOIN tb_categoria cat ON p.IdCategoria = cat.IdCategoria
    WHERE p.IdCategoria = @IdCategoria 
        AND p.Activo = 1
        AND cat.Activo = 1
    ORDER BY p.Nombre
    OFFSET (@Pagina - 1) * @RegistrosPorPagina ROWS
    FETCH NEXT @RegistrosPorPagina ROWS ONLY
END
GO

-- Productos por marca (con paginación)
CREATE PROCEDURE sp_ProductosPorMarca
    @IdMarca INT,
    @Pagina INT = 1,
    @RegistrosPorPagina INT = 12
AS
BEGIN
    SELECT 
        p.IdProducto,
        p.Nombre,
        p.Descripcion,
        p.Precio,
        p.Stock,
        p.UrlImagen,
        p.Activo,
        m.Descripcion AS NombreMarca,
        cat.Descripcion AS NombreCategoria,
        COUNT(*) OVER() AS TotalRegistros
    FROM tb_producto p
    INNER JOIN tb_marca m ON p.IdMarca = m.IdMarca
    INNER JOIN tb_categoria cat ON p.IdCategoria = cat.IdCategoria
    WHERE p.IdMarca = @IdMarca 
        AND p.Activo = 1
        AND m.Activo = 1
    ORDER BY p.Nombre
    OFFSET (@Pagina - 1) * @RegistrosPorPagina ROWS
    FETCH NEXT @RegistrosPorPagina ROWS ONLY
END
GO

-- Vista detallada de un producto
CREATE PROCEDURE sp_ProductoDetalle
    @IdProducto INT
AS
BEGIN
    SELECT 
        p.IdProducto,
        p.Nombre,
        p.Descripcion,
        p.Precio,
        p.Stock,
        p.UrlImagen,
        p.Activo,
        m.IdMarca,
        m.Descripcion AS NombreMarca,
        cat.IdCategoria,
        cat.Descripcion AS NombreCategoria,
        -- Información adicional
        (SELECT COUNT(*) FROM tb_carrito WHERE IdProducto = p.IdProducto) AS EnCarritos,
        (SELECT COUNT(*) FROM tb_detalle_venta WHERE IdProducto = p.IdProducto) AS VecesVendido
    FROM tb_producto p
    INNER JOIN tb_marca m ON p.IdMarca = m.IdMarca
    INNER JOIN tb_categoria cat ON p.IdCategoria = cat.IdCategoria
    WHERE p.IdProducto = @IdProducto 
        AND p.Activo = 1
END
GO

-- Verificar si usuario tiene tarjetas registradas
CREATE PROCEDURE sp_VerificarTarjetasUsuario
    @IdUsuario INT
AS
BEGIN
    SELECT 
        CASE 
            WHEN EXISTS (SELECT 1 FROM tb_tarjeta_pago WHERE IdUsuario = @IdUsuario AND Activo = 1) 
            THEN 'SI' 
            ELSE 'NO' 
        END AS TieneTarjetas,
        COUNT(*) AS CantidadTarjetas
    FROM tb_tarjeta_pago
    WHERE IdUsuario = @IdUsuario AND Activo = 1
END
GO

-- Listar tarjetas del usuario (para checkout)
CREATE PROCEDURE sp_ListarTarjetasUsuario
    @IdUsuario INT
AS
BEGIN
    SELECT 
        IdTarjeta,
        Tipo,
        LEFT(NumeroTarjeta, 4) + '****' + RIGHT(NumeroTarjeta, 4) AS NumeroTarjeta,
        Titular,
        FechaExpiracion,
        Predeterminada
    FROM tb_tarjeta_pago
    WHERE IdUsuario = @IdUsuario AND Activo = 1
    ORDER BY Predeterminada DESC
END
GO

-- Verificar autenticación y datos de usuario
CREATE PROCEDURE sp_VerificarUsuarioLogueado
    @Correo VARCHAR(255)
AS
BEGIN
    SELECT 
        u.IdUsuario,
        u.Nombres,
        u.Apellidos,
        u.Correo,
        u.Rol,
        u.Activo,
        CASE 
            WHEN EXISTS (SELECT 1 FROM tb_tarjeta_pago WHERE IdUsuario = u.IdUsuario AND Activo = 1) 
            THEN 1 
            ELSE 0 
        END AS TieneTarjetas,
        (SELECT COUNT(*) FROM tb_carrito WHERE IdUsuario = u.IdUsuario) AS ItemsCarrito
    FROM tb_usuario u
    WHERE u.Correo = @Correo AND u.Activo = 1
END
GO